
model{

# -------------------------------------------------
# Parameters:
# gamma: invasion probability
# eps: erradication probability
# p: probability of observing the plant
# -------------------------------------------------
# States (S):
# 1 empty
# 2 low abundance
# 3 high abundance 
# 
# Observations Dat M:  
# 1 not detected
# 2 low abundance
# 3 high abundance
#
# -------------------------------------------------

#### PRIORS ####
  #Erradication:
  eps.l0 ~ dbeta(eps.l0.a,eps.l0.b)T(0.0001,0.9999) #eradication when at low state
  eps.l1 ~ dnorm(eps.l1.mean, eps.l1.tau) #effect of eradication 
  eps.l1.tau <- 1/(eps.l1.sd * eps.l1.sd) #precision parameter
  
  eps.h0 ~ dbeta(eps.h0.a,eps.h0.b)T(0.0001,0.9999) #eradication when at high state
  eps.h1 ~ dnorm(eps.h1.mean, eps.h1.tau) #effect of eradication 
  eps.h1.tau <- 1/(eps.h1.sd * eps.h1.sd) #precision parameter
  
  #Invasion:
  gamma.0 ~dnorm(gamma.0.mean,gamma.0.tau) #intrinsic invasion probability
  gamma.0.tau <- 1/(gamma.0.sd*gamma.0.sd) #precision parameter
  gamma.1 ~dnorm(gamma.1.mean, gamma.1.tau) #effect of site characteristics on invasion probability
  gamma.1.tau <- 1/(gamma.1.sd*gamma.1.sd) #precision parameter
  gamma.2 ~dnorm(gamma.2.mean, gamma.2.tau) #effect of Neighboring invasion state
  gamma.2.tau <- 1/(gamma.2.sd*gamma.2.sd) #precision parameter
  
  #State transition:
  phi0.lh ~ dbeta(phi.lh.a, phi.lh.b)T(0.0001,0.9999) #transition from low to high
  phi1.lh ~ dnorm(phi.lh1.mean, phi.lh1.tau) #effect of removal on transition
  phi.lh1.tau <- 1/(phi.lh1.sd*phi.lh1.sd) #precision parameter
  
  phi0.hh ~ dbeta(phi.hh.a, phi.hh.b)T(0.0001,0.9999) #transition from high to high
  phi1.hh ~ dnorm(phi.hh1.mean, phi.hh1.tau) #effect of removal on transition
  phi.hh1.tau <- 1/(phi.hh1.sd*phi.hh1.sd) #precision parameter
  
  #Detection low state:
  p.l0 ~ dbeta(p.l0.a, p.l0.b)T(0.0001,0.9999) #base detection for low state
  p.l1 ~ dnorm(p.l1.mean, p.l1.tau) #effect of effort 
  p.l1.tau <- 1/(p.l1.sd * p.l1.sd) #precision parameter
  alpha.l ~ dnorm(l.mean,l.tau) #difference in baseline detection between dat D and M
  l.tau <- 1/(l.sd * l.sd) #precision
  
  #Detection high state:
  p.h0 ~ dbeta(p.h0.a, p.h0.b)T(0.0001,0.9999) #base detection for high state
  p.h1 ~ dnorm(p.h1.mean, p.h1.tau) #effect of effort 
  p.h1.tau <- 1/(p.h1.sd * p.h1.sd) #precision parameter
  alpha.h ~ dnorm(h.mean,h.tau) #difference in baseline detection between dat D and M
  h.tau <- 1/(h.sd * h.sd) #precision

  logit(pM.l) <- p.l0 + p.l1*logeffort + alpha.l #detection low state
  logit(pM.h) <- p.h0 + p.h1*logeffort + alpha.h #detection high state
  
  #initial occupancy probabilities
  psi[1:3] ~ ddirch(alpha) #alpha = rep(1,3)
  
#--------------------------------------------------#
# STATE TRANSITION
for (i in 1:n.sites){  
  # State transition probabilities (TPM): probability of S(t+1) given S(t)
  for (t in 1:n.weeks){
  
    logit(gamma[i,t]) <-gamma.0 + gamma.1*site.char[i] + gamma.2*D[i,t] #invasion probability
    logit(eps.l[i,t]) <- eps.l0 + eps.l1*rem.vec[i,t]*removal.hours[2] #erradication low state
    logit(eps.h[i,t]) <- eps.h0 + eps.h1*rem.vec[i,t]*removal.hours[3] #erradication high state
                                        # rem.vec[i] = 0,1 if 0, then no removal and no erradiction
    
    logit(phi.lh[i,t]) <- phi0.lh - phi1.lh*rem.vec[i,t]*removal.hours[2]
    logit(phi.hh[i,t]) <- phi0.hh - phi1.hh*rem.vec[i,t]*removal.hours[3]
    
    #index = [current state, location, time, future state]
    #empty stay empty
    TPM[1,i,t,1] <- 1-gamma[i,t] #1-gamma = not invasion probability
    
    #empty to low abundance
    TPM[1,i,t,2] <- gamma[i,t] #invasion probability
    
    #empty to high abundance
    TPM[1,i,t,3] <- 0 #invasion probability

    #low abundance to empty
    TPM[2,i,t,1] <- eps.l[i,t] #erradication probability
                                      
    #low abundance to low abundance
    TPM[2,i,t,2] <- (1- eps.l[i,t])*(1-phi.lh[i,t]) #erradication failure probability
    
    #low abundance to high abundance
    TPM[2,i,t,3] <- (1- eps.l[i,t])*(phi.lh[i,t])
    
    #high abundance to empty
    TPM[3,i,t,1] <- eps.h[i,t] #erradication probability
    
    #high abundance to low abundance
    TPM[3,i,t,2] <- (1- eps.h[i,t])*(1-phi.hh[i,t]) #erradication failure probability
    
    #high abundance to high abundance
    TPM[3,i,t,3] <- (1- eps.h[i,t])*(phi.hh[i,t])
    
    #--------------------------------------------------#
    # OBSERVATION PROBABILITIES (for multi state detection/nondetection data)
    
    for(j in 1:n.occs){

      #Empty and not observed  
      P.datM[1,i,j,t,1] <- 1
      
      #Empty and observed low 
      P.datM[1,i,j,t,2] <- 0
      
      #Empty and observed high
      P.datM[1,i,j,t,3] <- 0
   
      #Low state and not observed
      P.datM[2,i,j,t,1] <- 1-pM.l #not detected probability low state
      
      #Low state and observed low
      P.datM[2,i,j,t,2] <- pM.l #detection probability low state
      
      #Low state and observed high
      P.datM[2,i,j,t,3] <- 0 #detection probability low state
      
      #High state and not observed
      P.datM[3,i,j,t,1] <- 1-pM.h #not detected probability high state
      
      #High state and observed low
      P.datM[3,i,j,t,2] <- 0
      
      #High state and observed high
      P.datM[3,i,j,t,3] <- pM.h
      
    } #j 
  } #t
} #i

  #### Likelihood ####
  for (i in 1:n.sites){
      
    #-- Initial State: --# 
    
      State[i,1] ~ dcat(psi) #psi is written above in the priors
    
      D[i,1] <- sum(State[neighbors[i,], 1])/n.neighbors[i] #state of neighbors 
    
    #-- State Model: --#
    for (t in 2:n.weeks){ 
      # State process: state given previous state and transition probability
      State[i,t] ~ dcat(TPM[State[i,t-1], i, t-1, ]) 
      
      #below is not correct because it does not lead to a true state: 
      D[i,t] <- sum(State[neighbors[i,], t])/n.neighbors[i] #state of neighbors 
       
     
    } #t loop

    #----- Observation Model -----#
    for(j in 1:n.occs){
      for(t in 1:n.weeks){
        # Observation process: draw observation given current state
        yM[i,j,t] ~ dcat(P.datM[State[i,t], i, j, t,]) 
        
      } #t
    } #j

    #Derived parameter: final estimated state
    State.fin[i] <- State[i,n.weeks] #state after 4 weeks
    
  } #i


} #end model

